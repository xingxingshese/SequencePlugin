import org.jetbrains.changelog.markdownToHTML

fun properties(key: String) = project.findProperty(key).toString()

buildscript {
    repositories {
        maven("https://maven.aliyun.com/repository/public")
        maven("https://maven.aliyun.com/repository/gradle-plugin")
    }
}

plugins {
    id("java")
    id("org.jetbrains.intellij") version "1.9.0"
    id("org.jetbrains.kotlin.jvm") version "1.7.10"
    id("org.jetbrains.changelog") version "1.3.1"
}

group = properties("pluginGroup")
version = properties("pluginVersion")

repositories {
    mavenLocal()
    google()
    maven("https://maven.aliyun.com/repository/public")
    maven("https://maven.aliyun.com/repository/gradle-plugin")
    maven("https://www.jetbrains.com/intellij-repository/releases")
    maven("https://cache-redirector.jetbrains.com/intellij-dependencies")
}

intellij {
    version.set(properties("platformVersion"))
    type.set(properties("platformType"))
//    localPath.set("/Applications/IntelliJ IDEA CE.app")
    pluginName.set(properties("pluginName"))
    plugins.set(properties("platformPlugins").split(',').map(String::trim).filter(String::isNotEmpty))
    updateSinceUntilBuild.set(true)
    sandboxDir.set("${project.rootDir}/.sandbox")
    downloadSources.set(properties("platformDownloadSources").toBoolean())
}

changelog {
    version.set(properties("pluginVersion"))
    groups.set(emptyList())
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
}

tasks {
//    withType<JavaCompile> {
//        options.encoding = "UTF-8"
//        sourceCompatibility = "1.9"
//        targetCompatibility = "1.9"
//    }

    patchPluginXml {
        version.set(properties("pluginVersion"))
        sinceBuild.set(properties("pluginSinceBuild"))
        untilBuild.set(properties("pluginUntilBuild"))
        pluginDescription.set(
            File(projectDir, "README.md").readText().lines().run {
                val start = "<!-- Plugin description -->"
                val end = "<!-- Plugin description end -->"

                if (!containsAll(listOf(start, end))) {
                    throw GradleException("Plugin description section not found in README.md:\n$start ... $end")
                }
                subList(indexOf(start) + 1, indexOf(end))
            }.joinToString("\n").run { markdownToHTML(this) }
        )
        changeNotes.set(provider { changelog.getLatest().toHTML() })
    }

//    runPluginVerifier {
//        ideVersions.set(properties("pluginVerifierIdeVersions").split(',').map(String::trim).filter(String::isNotEmpty))
//    }

    publishPlugin {
        dependsOn("patchChangelog")
        token.set(System.getenv("PUGLISH_TOKEN"))
        // pluginVersion is based on the SemVer (https://semver.org) and supports pre-release labels, like 2.1.7-alpha.3
        // Specify pre-release label to publish the plugin in a custom Release Channel automatically. Read more:
        // https://plugins.jetbrains.com/docs/intellij/deployment.html#specifying-a-release-channel
        channels.set(listOf(properties("pluginVersion").split('-').getOrElse(1) { "default" }.split('.').first()))
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "11"
    }

    compileTestKotlin {
        kotlinOptions.jvmTarget = "11"
    }
}

dependencies {
//    implementation("ar.com.hjg:pngj:2.1.0")
    implementation("org.apache.xmlgraphics:batik-svggen:1.14")
}